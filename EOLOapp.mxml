<?xml version="1.0" encoding="utf-8"?>
<s:WindowedApplication xmlns:fx="http://ns.adobe.com/mxml/2009"
					   xmlns:s="library://ns.adobe.com/flex/spark"
					   xmlns:mx="library://ns.adobe.com/flex/mx"
					   width="365" height="222" backgroundColor="#EDEDED"
					   applicationComplete="responseRequest.send();refreshData();initApp();checkForUpdate();hideFields()"
					   showStatusBar="false">
	<fx:Declarations>
		<s:HTTPService id="responseRequest" url="https://care.ngi.it/ws/ws.asp" useProxy="false" method="GET" resultFormat="text"
					   fault="Alert.show('Impossibile leggere i dati. Verificare la connessione internet.','Errore',mx.controls.Alert.OK,this,closeAppOnErr,iconError);" result="responseJSON(event)">
			<mx:request xmlns="">
				<!-- Parametri per la GET -->
				<a>get.quota</a>
			</mx:request>
		</s:HTTPService>
	</fx:Declarations>
	<fx:Script>
		<![CDATA[
			
			// Import per la gestione di link
			import flash.net.navigateToURL;
			import flash.net.URLRequest;
			
			// Import per la gestione dell'update
			import air.update.ApplicationUpdaterUI;
			import air.update.events.UpdateEvent;
			
			// Import per la gestione del JSON
			import com.adobe.serialization.json.JSON; // https://github.com/mikechambers/as3corelib
			
			// Import per la gestione degli alert e degli eventi
			import mx.controls.Alert;
			import mx.events.CloseEvent;
			import mx.rpc.events.ResultEvent;
			
			// Import per la gestione degli offset
			import mx.geom.TransformOffsets;
			// Define an instance of TransformOffsets.
			private var XForm1:TransformOffsets = new TransformOffsets();
			private function initOffsets1():void {
				voiceTxt.postLayoutTransformOffsets = XForm1; 
			}
			private var XForm2:TransformOffsets = new TransformOffsets();
			private function initOffsets2():void {
				voiceTxtLabel.postLayoutTransformOffsets = XForm2; 
			}
			
			// Inizializzo l'icona per gli errori
			[Embed(source="assets/iconError.png")] public var iconError:Class;

			// Funzione per accedere alla pagina per ricaricare il traffico
			public function gotoLink():void {
				navigateToURL(new URLRequest("http://www.ngi.it/it/connettivita/eolo-wireless/listini/accessori/"), "_blank");
			}
			
			// Funzione per il refresh dei dati temporizzato
			private function refreshData():void {
				var minuteTimer:Timer = new Timer(300000, 0); // Esecuzione ogni 300000ms (5 minuti) fino a infinito (0)
				minuteTimer.addEventListener(TimerEvent.TIMER, onTick); // Listener per l'azione da eseguire
				minuteTimer.start(); // Faccio partire il counter per il refresh
			}
			// Trigger per il refresh dei dati
			public function onTick(event:TimerEvent):void {
				responseRequest.send();
			}

			// Funzione per nascondere, all'apertura dell'APP, tutti i campi
			private function hideFields():void {
				usedTxt.visible = false;
				usedTxtLabel.visible = false;
				remainTxt.visible = false;
				remainTxtLabel.visible = false;
				nextResetTxt.visible = false;
				nextResetTxtLabel.visible = false;
				voiceTxt.visible = false;
				voiceTxtLabel.visible = false;
				labelIva.visible = false;
			}
			
			// Funzione per prelevare e parsare il responso
			private function responseJSON(event:ResultEvent):void {
				
				// Prelevo il JSON e lo converto in stringa
				var rawData:String = String(event.result);
				var responseJson:Object = JSON.decode(rawData);
				
				// Controllo il responso
				if(responseJson.response.status == 400) {
					Alert.show("Questa connessione non e' compatibile con EOLOapp",'Errore',mx.controls.Alert.OK,this,closeAppOnErr,iconError);
				} else if(responseJson.response.status == 200) {
					// Eseguo i calcoli sui valori, facendo il cast corretto
					var usedValue:int = responseJson.data.used/1024;
					var quotaValue:int = responseJson.data.quota/1024;
					var dispoValue:int = quotaValue - usedValue;
					// Se il traffico consumato e' maggiore de disponibile, allora forzo il disponibile a 0
					if(dispoValue <= 0) {
						dispoValue = 0;
					}
					
					// Valorizzo i campi sulla base di quanto prelevato dal JSON
					if(usedValue > 0 || dispoValue > 0) {
						usedTxt.visible = true;
						usedTxtLabel.visible = true;
						remainTxt.visible = true;
						remainTxtLabel.visible = true;
						nextResetTxt.visible = true;
						nextResetTxtLabel.visible = true;
						usedTxt.text = usedValue.toString() + " MB"; // Traffico utilizzato float()
						remainTxt.text = dispoValue.toString() + " MB"; // Traffico rimanente float()
						nextResetTxt.text = responseJson.data.nextReset.toString(); // Riazzeramento soglie di traffico string()
					}
					
					// Se il valore del traffico voce è "-9999" vuole dire che la voce non e' abilitata
					if(responseJson.voice.credit.toFixed(2) != -9999) {
						voiceTxt.visible = true;
						voiceTxtLabel.visible = true;
						labelIva.visible = true;
						voiceTxt.text = responseJson.voice.credit.toFixed(2) + " €";
					}
					
					// Se non ci sono valori di traffico, allora la connessione ha solo la voce abilitata; sposto le etichette per centrarle
					if(usedValue == 0 && dispoValue == 0 && responseJson.voice.credit.toFixed(2) != -9999) {
						voiceTxt.postLayoutTransformOffsets.y = voiceTxt.postLayoutTransformOffsets.y - 50;
						voiceTxtLabel.postLayoutTransformOffsets.y = voiceTxtLabel.postLayoutTransformOffsets.y - 50;
					}
					
					// Cambio il colore dell'icona in funzione del traffico disponibile
					var percentUsed:int = (usedValue/quotaValue) * 100;
					if(percentUsed <= 80)
						trafficIcon.source = 'assets/iconGreen.png';
					else if(percentUsed < 100)
						trafficIcon.source = 'assets/iconOrange.png';
					else if(percentUsed >= 100) {
						trafficIcon.source = 'assets/iconRed.png';
						notify();
					}
				} else {
					Alert.show('Si e\' verificato un errore. Riprovare piu\' tardi','Errore',mx.controls.Alert.OK,this,closeAppOnErr,iconError);
				}
			}
			
			// Funzione per la chiusura dell'Applicazione a chiusura della finestra di Alert
			private function closeAppOnErr(event:CloseEvent):void {
				NativeApplication.nativeApplication.exit();
			}
			
			/*************************
			 * Gestione degli update *
			 *************************/
			// Inizializzo UI per gestione update
			private var updater:ApplicationUpdaterUI = new ApplicationUpdaterUI();
			
			// Inizializzo updater
			private function checkForUpdate():void{
				updater.updateURL = "http://www.eolo.it/eoloApp/update.xml";
				updater.isCheckForUpdateVisible = false;
				updater.addEventListener(UpdateEvent.INITIALIZED, updaterInitialized);
				updater.initialize();
			}
			
			// Verifico la presenza di aggiornamenti
			private function updaterInitialized(event:UpdateEvent):void{
				updater.checkNow();
			}
			
			/*********************************
			 * Gestione Icona sysTray o dock *
			 *********************************/
			private var trayIcon:BitmapData; // Crea la variabile di tipo immagine per l'icona
			
			// Inizializzazione del blocco per la Dock o SysTray
			public function initApp():void {
				var loader:Loader = new Loader(); // Inizializza il loader per l'icona
				this.addEventListener(Event.CLOSING, closeApp); // Intercetta la chiusura dell'APP
				loader.contentLoaderInfo.addEventListener(Event.COMPLETE, readyToTray);
				// Usa una differente icona a seconda del Sistema Operativo
				if (NativeApplication.supportsSystemTrayIcon)
					loader.load(new URLRequest("assets/icon16.png"));
				else if (NativeApplication.supportsDockIcon)
					loader.load(new URLRequest("assets/icon128.png"));
				else
					loader.load(new URLRequest("assets/icon16.png"));
			}
			
			// Funzione per la riduzione a Dock o SysTray
			private function minToTray(event:Event):void {
				dock();
			}
			
			// Gestione degli eventi per il menu e la riduzione a Dock o SysTray
			public function readyToTray(event:Event):void {
				trayIcon = event.target.content.bitmapData; // Riempie l'oggetto trayIcon con l'icona
				
				var myMenu:NativeMenu = new NativeMenu(); // Crea il menu
				
				var openItem:NativeMenuItem = new NativeMenuItem("Apri"); // Crea la voce di riapertura dell'APP nel menu
				var closeItem:NativeMenuItem = new NativeMenuItem("Chiudi applicazione"); // Crea la voce di chiusura dell'APP nel menu
				
				openItem.addEventListener(Event.SELECT, unDock); // Gestione del click su menu per riapertura dell'APP
				closeItem.addEventListener(Event.SELECT, closeApp); // Gestione del click su menu per chiusura dell'APP
				
				myMenu.addItem(openItem); // Inserisce menu per riaprire l'APP
				myMenu.addItem(new NativeMenuItem("", true)); // Inserisce una riga vuota
				myMenu.addItem(closeItem); // Inseriesce menu per la chiusura dell'APP

				if (NativeApplication.supportsSystemTrayIcon) { // Per Windows
					NativeApplication.nativeApplication.autoExit = false;
					SystemTrayIcon(NativeApplication.nativeApplication.icon).tooltip = "EOLOapp"; // Tooltip dell'app in SysTray
					SystemTrayIcon(NativeApplication.nativeApplication.icon).addEventListener(MouseEvent.CLICK, unDock);
					stage.nativeWindow.addEventListener(NativeWindowDisplayStateEvent.DISPLAY_STATE_CHANGING, winMinimized);
					SystemTrayIcon(NativeApplication.nativeApplication.icon).menu = myMenu; // Aggiunge il menu
				}
				else if (NativeApplication.supportsDockIcon) { // Per OSX
					DockIcon(NativeApplication.nativeApplication.icon).addEventListener(InvokeEvent.INVOKE, unDock);
					stage.nativeWindow.addEventListener(NativeWindowDisplayStateEvent.DISPLAY_STATE_CHANGING, winMinimized);
					DockIcon(NativeApplication.nativeApplication.icon).menu = myMenu; // Aggiunge il menu
				}
			}
			
			// Verifica se l'APP e' in Dock o SysTray
			private function winMinimized(displayStateEvent:NativeWindowDisplayStateEvent):void {
				if(displayStateEvent.afterDisplayState == NativeWindowDisplayState.MINIMIZED) {
					dock();
				}
			}
			
			// Riduce nella Dock o nel SysTray l'APP
			public function dock():void {
				stage.nativeWindow.visible = false;
				NativeApplication.nativeApplication.icon.bitmaps = [trayIcon];
			}
			
			// Riapertura dell'APP quando e in Dock o SysTray
			public function unDock(event:Event):void {
				stage.nativeWindow.visible = true;
				stage.nativeWindow.orderToFront();
				stage.nativeWindow.activate();
				stage.nativeWindow.restore();
				NativeApplication.nativeApplication.icon.bitmaps = [];
			}
			
			// Gestione della chiusura dell'APP
			private function closeApp(event:Event):void {
				NativeApplication.nativeApplication.exit();
			}
			
			// Bounce dell'icona se si supera la quota
			public function notify():void{
				if(NativeApplication.supportsDockIcon){
					var dock:DockIcon = NativeApplication.nativeApplication.icon as DockIcon;
					dock.bounce(NotificationType.CRITICAL);
				} else if (NativeApplication.supportsSystemTrayIcon){
					stage.nativeWindow.notifyUser(NotificationType.CRITICAL);
				}
			}
		]]>
	</fx:Script>
	<s:Rect id="backgroundRect" left="0" right="0" top="0" bottom="0">
		<s:fill>
			<s:LinearGradient rotation="90">
				<s:GradientEntry color="#4AAEF0" />
				<s:GradientEntry color="#285DAA" />
			</s:LinearGradient>
		</s:fill>
	</s:Rect>
	<s:Label id="remainTxt" x="249" y="119.2" width="86" height="18" color="#FFFFFF"
			 fontFamily="Verdana" fontSize="13" fontStyle="italic" fontWeight="bold"
			 textAlign="left" verticalAlign="middle"/>
	<s:Label id="nextResetTxt" x="249" y="139.2" width="106" height="30" color="#FFFFFF"
			 fontFamily="Verdana" fontSize="13" fontStyle="italic" fontWeight="bold"
			 textAlign="left" verticalAlign="middle"/>
	<s:Label id="voiceTxt" x="249" y="171.2" width="86" height="18" color="#FFFFFF"
			 fontFamily="Verdana" fontSize="13" fontStyle="italic" fontWeight="bold"
			 textAlign="left" verticalAlign="middle" creationComplete="initOffsets1();"/>
	<s:Label x="119" y="170.5" width="130" height="18" chromeColor="#CCCCCC" color="#FFFFFF"
			 fontFamily="Verdana" text="Credito EOLO Voce:" textAlign="left" verticalAlign="middle"
			 id="voiceTxtLabel" creationComplete="initOffsets2();"/>
	<s:Image left="0" bottom="0" width="103" height="107" scaleMode="stretch" smooth="true"
			 smoothingQuality="high" source="@Embed('assets/bottomSx.png')"/>
	<s:Image left="8" top="9" width="150" height="82" scaleMode="stretch" smooth="true"
			 smoothingQuality="high" source="@Embed('assets/logo.png')"/>
	<s:Label id="usedTxtLabel" x="119" y="95" width="130" height="18" chromeColor="#CCCCCC"
			 color="#FFFFFF" fontFamily="Verdana" text="Traffico consumato:" textAlign="left"
			 verticalAlign="middle"/>
	<s:Label id="labelIva" x="193" y="197" width="167" height="11" chromeColor="#CCCCCC"
			 color="#FFFFFF" fontFamily="Verdana" fontSize="9"
			 text="Il credito indicato è IVA inclusa" textAlign="right" verticalAlign="middle"/>
	<s:Label id="labelUpdate" x="160" y="208" width="200" height="11" chromeColor="#CCCCCC"
			 color="#FFFFFF" fontFamily="Verdana" fontSize="9"
			 text="I dati sono aggiornati ogni 2 ore" textAlign="right" verticalAlign="middle"/>
	<s:Label id="remainTxtLabel" x="119" y="119.45" width="130" height="18" chromeColor="#CCCCCC"
			 color="#FFFFFF" fontFamily="Verdana" text="Traffico disponibile:" textAlign="left"
			 verticalAlign="middle"/>
	<s:Label id="nextResetTxtLabel" x="119" y="145" width="130" height="18" chromeColor="#CCCCCC"
			 color="#FFFFFF" fontFamily="Verdana" text="Scadenza traffico:" textAlign="left"
			 verticalAlign="middle"/>
	<s:Label id="usedTxt" x="249" y="95.2" width="86" height="18" color="#FFFFFF"
			 fontFamily="Verdana" fontSize="13" fontStyle="italic" fontWeight="bold"
			 textAlign="left" verticalAlign="middle"/>
	<s:Image id="trafficIcon" right="14" top="13" width="60" height="60" scaleMode="letterbox" smooth="true" source="assets/iconGreen.png"/>
	<s:Image x="170" y="25" width="115" height="32" scaleMode="letterbox" smooth="true"
			 smoothingQuality="high" source="@Embed('assets/pulsante.png')"/>
	<s:Label x="170" y="25" width="102" height="32" click="gotoLink()"
			 color="#009FDA" fontFamily="Verdana" fontSize="11" mouseChildren="false"
			 text="Ricarica traffico" textAlign="center" useHandCursor="true" buttonMode="true" verticalAlign="middle"/>
</s:WindowedApplication>